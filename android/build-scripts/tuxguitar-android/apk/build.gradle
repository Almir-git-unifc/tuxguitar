apply plugin: 'com.android.application'

def plugins = [
	'TuxGuitar-android-gervill-plugin',
	'TuxGuitar-android-midi-plugin',
	'TuxGuitar-android-browser-saf-plugin',
]

def plugins_common = [
		'TuxGuitar-compat',
		'TuxGuitar-gtp',
		'TuxGuitar-gpx',
		'TuxGuitar-ptb',
		'TuxGuitar-midi',
		'TuxGuitar-lilypond',
		'TuxGuitar-pdf',
]


android {

	namespace = 'org.herac.tuxguitar.android.application'

	compileSdkVersion 30
	buildToolsVersion "30.0.3"

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	defaultConfig {
		applicationId "org.herac.tuxguitar.android.application"
		minSdkVersion 24
		targetSdkVersion 30
		multiDexEnabled true
		versionCode Integer.parseInt(new Date().format('yyMMddHH'))
		versionName "9.99-SNAPSHOT"
		setProperty("archivesBaseName", "tuxguitar-android-9.99-SNAPSHOT")
	}

	buildTypes {
		release {
			zipAlignEnabled true
		}
	}

	sourceSets {
		main {
			java.srcDirs = ['./build/generated/tuxguitar-android-apk-src/src']
			resources.srcDirs = ['./build/generated/tuxguitar-android-apk-src/share']
		}
	}

	lintOptions {
		checkReleaseBuilds false
	}
}

task generateSources {
 	// copy plugins to tmp folder
	plugins.each { String plugin ->
		copy {
			from '../../../' + plugin
			into './build/generated/tuxguitar-android-apk-src'
			
			exclude '**/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin'
		}
	}
	plugins_common.each { String plugin ->
		copy {
			from '../../../../common/' + plugin
			into './build/generated/tuxguitar-android-apk-src'

			exclude '**/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin'
		}
	}

	// create unified /META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin file
	List<String> fileNames = new ArrayList<String>()
	plugins.each { String plugin ->
		fileNames.add('../../../' + plugin + '/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')
	}
	plugins_common.each { String plugin ->
		fileNames.add('../../../../common/' + plugin + '/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin')
	}
	File file = new File('apk/build/generated/tuxguitar-android-apk-src/share/META-INF/services/org.herac.tuxguitar.util.plugin.TGPlugin');
 	file.getParentFile().mkdirs()
 	file.text = files(fileNames).collect{it.getText()}.join("\n")
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation project(':tuxguitar-android')
	implementation project(':tuxguitar-android-resources')
	implementation project(':tuxguitar-android-gervill')
	implementation project(':tuxguitar-android-midi')
	implementation project(':tuxguitar-android-browser-saf')
	implementation project(':tuxguitar-android-gervill-soundbank')
	
	implementation 'com.itextpdf:itextpdf:5.5.13.3'
	implementation 'com.itextpdf.tool:xmlworker:5.5.13.1'
	implementation 'org.apache.commons:commons-compress:1.26.0'
	implementation 'commons-io:commons-io:2.16.1'
	implementation 'org.apache.commons:commons-lang3:3.17.0'
}
